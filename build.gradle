plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
}

repositories {

    mavenLocal()

    maven {
        url = uri('https://oss.sonatype.org/content/repositories/snapshots')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2')
    }
}

dependencies {
    
    testImplementation 'com.h2database:h2:1.4.192'
    testImplementation 'mysql:mysql-connector-java:6.0.3'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.testng:testng:7.0.0'
    testImplementation 'org.apache.commons:commons-lang3:3.3.2'
    testImplementation 'org.slf4j:slf4j-api:1.7.5'
    testImplementation 'org.projectlombok:lombok:1.18.12'
    testImplementation 'jakarta.validation:jakarta.validation-api:2.0.2'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    
    compileOnly 'org.projectlombok:lombok:1.18.12'
    compileOnly 'jakarta.validation:jakarta.validation-api:2.0.2'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    
}

group = 'com.kasisoft'
version = '3.7-SNAPSHOT'
description = 'KCL :: Kasisoft Common Library'
buildDir = 'target'
sourceCompatibility = '14'
compileJava.options.fork = true
javadoc.options.addStringOption('Xdoclint:none', '-quiet')

configurations.all {

    resolutionStrategy {

        failOnVersionConflict()

        preferProjectModules()

    }
  
}

jacoco {
    toolVersion = '0.8.5'
    reportsDir = file("$buildDir/customJacocoReportDir")
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('--enable-preview') 
}

test {
    useTestNG()
    jvmArgs(['--enable-preview'])
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}
