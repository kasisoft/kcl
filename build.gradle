plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'io.freefair.lombok' version '5.1.0'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group = 'com.kasisoft'
description = 'KCL :: Kasisoft Common Library'
version = '4.2-SNAPSHOT'
def isSnapshot = version.endsWith('-SNAPSHOT')
def isRelease = !isSnapshot;
buildDir = 'target'
sourceCompatibility = '14'
compileJava.options.fork = true

if (isRelease) {
    def pw = findProperty('signing.password')
    if ((pw == null) || (pw.length() == 0)) {
        throw new GradleException('Signing for a release requires as password (signing.password)')
    }
    def keyId = findProperty('signing.keyId')
    if ((keyId == null) || (keyId.length() == 0)) {
        throw new GradleException('Signing for a release requires as keyId (signing.keyId)')
    }
    def ringFile = findProperty('signing.secretKeyRingFile')
    if ((ringFile == null) || (ringFile.length() == 0)) {
        throw new GradleException('Signing for a release requires as key ring file (signing.secretKeyRingFile; binary version)')
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    
    testImplementation 'com.h2database:h2:1.4.192'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.testng:testng:7.0.0'
    testImplementation 'jakarta.validation:jakarta.validation-api:2.0.2'
    
    compileOnly 'jakarta.validation:jakarta.validation-api:2.0.2'
    
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

configurations.all {

    resolutionStrategy {

        failOnVersionConflict()

        preferProjectModules()

    }
  
}

lombok {
  version = '1.18.12'
}

jacoco {
    toolVersion = '0.8.5'
    reportsDir = file("$buildDir/customJacocoReportDir")
}

java {
    withSourcesJar()
    withJavadocJar()
}

// https://docs.gradle.org/current/samples/sample_java_modules_multi_project.html [03-JUL-2020]
plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
    }
}
    
publishing {

    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
    
    repositories {
    
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/kasisoft/kcl'
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
        
    }
    
}

generatePomFileForMavenPublication {
    
    pom.name = 'KCL :: Kasisoft Common Library'
    pom.description = 'Collection of general purpose functionalities.'
    pom.url = 'https://kasisoft.github.io/kcl/'
    
    pom.organization({
        name = 'Kasisoft'
        url = 'https://kasisoft.com'
    })
    
    pom.license({
        name = 'MIT License'
        url = 'https://choosealicense.com/licenses/mit'
    })
    
    pom.developer({
        id = 'costamojan'
        name = 'Daniel Kasmeroglu'
        email = 'daniel.kasmeroglu@kasisoft.com'
    })
    
    pom.scm({
        connection = 'scm:git:https://github.com/kasisoft/kcl.git'
        developerConnection = 'scm:git:https://github.com/kasisoft/kcl.git'
        url = 'https://kasisoft.github.io/kcl'
    })
    
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javadoc.options {
    addStringOption('-release', '14')
}

test {
    exclude 'com/kasisoft/libs/common/graphics/**'
    useTestNG()
    jvmArgs(['-Duser.country=DE', '-Duser.language=de'])
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

signing {
    required { isRelease }
    sign publishing.publications.maven
}
