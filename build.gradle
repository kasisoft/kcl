plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id 'io.freefair.lombok' version '5.1.0'
}

group = 'com.kasisoft'
version = '3.8-SNAPSHOT'
description = 'KCL :: Kasisoft Common Library'
buildDir = 'target'
sourceCompatibility = '14'
compileJava.options.fork = true

repositories {

    mavenLocal()

    maven {
        url = uri('https://oss.sonatype.org/content/repositories/snapshots')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2')
    }
}

dependencies {
    
    testImplementation 'com.h2database:h2:1.4.192'
    testImplementation 'mysql:mysql-connector-java:6.0.3'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.testng:testng:7.0.0'
    testImplementation 'org.apache.commons:commons-lang3:3.3.2'
    testImplementation 'org.slf4j:slf4j-api:1.7.5'
    testImplementation 'jakarta.validation:jakarta.validation-api:2.0.2'
    
    compileOnly 'jakarta.validation:jakarta.validation-api:2.0.2'
    
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

configurations.all {

    resolutionStrategy {

        failOnVersionConflict()

        preferProjectModules()

    }
  
}

lombok {
  version = '1.18.12'
}

jacoco {
    toolVersion = '0.8.5'
    reportsDir = file("$buildDir/customJacocoReportDir")
}

java {
    withSourcesJar()
    withJavadocJar()
}

// https://docs.gradle.org/current/samples/sample_java_modules_multi_project.html [03-JUL-2020]
plugins.withType(JavaPlugin).configureEach {
    java {
        modularity.inferModulePath = true
    }
}
    
publishing {

    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }
    
    repositories {
    
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/kasisoft/kcl'
            credentials {
                username = System.getenv('GITHUB_ACTOR')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
        
    }
    
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

javadoc.options {
    addStringOption('-release', '14')
}

test {
    exclude 'com/kasisoft/libs/common/graphics/**'
    useTestNG()
    jvmArgs(['-Duser.country=DE', '-Duser.language=de'])
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}
